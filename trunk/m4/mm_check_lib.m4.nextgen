# This file is part of a progressive rewrite of the GNU (autotools) build
# scripts. Please do not modify this file. Instead, please modify the legacy
# build files (without the .nextgen suffix). Changes to legacy files will be
# automatically detected and manually applied to the next-gen files.
# - Mark Tsuchida
#
# %nextgen_build_filename = mm_check_lib.m4

# Author: Mark Tsuchida
# Copyright: University of California, San Francisco, 2014
# License: BSD

# TODO Support for C++ libraries, with CXXFLAGS, CXXCPPFLAGS (but most of the
# libraries we use are C libraries)

# $1 - flag var prefix, e.g. LIBFOO
# $2 - display name, e.g. libfoo
# $3 - variable name suffix, e.g. libfoo (may contain underscores)
# $4 - include directory to prepend to CPPFLAGS (TODO: maybe support multiple)
# $5 - library directory to prepend to LDFLAGS (TODO: maybe support multiple)
# $6 - default library flags, e.g. -lfoo
# $7 - header to check for, e.g. foo/foo.h
# $8 - library function to check for, e.g. foo_init

# Output variables
# have_$3 (e.g. have_libfoo) - yes or no
# $1_CPPFLAGS (e.g. LIBFOO_CPPFLAGS)
# $1_CFLAGS (e.g. LIBFOO_CFLAGS)
# $1_LDFLAGS (e.g. LIBFOO_LDFLAGS)
# $1_LIBS (e.g. LIBFOO_LIBS)

AC_DEFUN([MM_CHECK_LIB], [

# "Precious" variables to allow user override of automatic configuration
AC_ARG_VAR([$1_CPPFLAGS], [preprocessor flags for $2])
AC_ARG_VAR([$1_CFLAGS], [compiler flags for $2])
AC_ARG_VAR([$1_LDFLAGS], [linker flags for $2])
AC_ARG_VAR([$1_LIBS], [library flags for $2])

# If any of the precious variables are already defined, do not modify them.
# Assume the user knows what they are doing.
AC_MSG_CHECKING([for user-provided configuration for $2])
test -n "$$1_CPPFLAGS" && have_$3=yes
test -n "$$1_CFLAGS" && have_$3=yes
test -n "$$1_LDFLAGS" && have_$3=yes
test -n "$$1_LIBS" && have_$3=yes

AS_IF([test "x$have_$3" = xyes],
[
   AC_MSG_RESULT([yes; will skip checks])
],
[
   AC_MSG_RESULT([no; will check for availability])

   # Defaults
   have_$3=
   m4_ifval([$4], [test -n "$4" && $1_CPPFLAGS="-I$4"])
   m4_ifval([$5], [test -n "$5" && $1_LDFLAGS="-L$5"])
   $1_LIBS="$6"

   MM_LIB_IFELSE([$1], [$2], [], [$7], [$8], [have_$3=yes], [have_$3=no])

   # Don't leak trid but failed configuration
   if "x$have_$3" = xno; then
      $1_CPPFLAGS=
      $1_CFLAGS=
      $1_LDFLAGS=
      $1_LIBS=
   fi
])
])
