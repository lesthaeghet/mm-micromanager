# This file is part of a progressive rewrite of the GNU (autotools) build
# scripts. Please do not modify this file. Instead, please modify the legacy
# build files (without the .nextgen suffix). Changes to legacy files will be
# automatically detected and manually applied to the next-gen files.
# - Mark Tsuchida
#
# %nextgen_build_filename = mm_lib_opencv.m4
# %nextgen_build_replaces = ax_opencv.m4 904b6d2016853877d07a19ce948445c6

# Author: Mark Tsuchida
# Copyright: University of California, San Francisco, 2014
# License: BSD

# Check for OpenCV video capture
#
# Usage: MM_LIB_OPENCV([OpenCV prefix], [action-if-found],
# [action-if-not-found])
#
# First try to get the required flags from pkg-config. Since the pkg-config
# entry tends to be erroneous (especially on OS X), we verify that the link
# succeeds and fall back to just listing -l flags.
#
# Trial 1: get OPENCV_* from pkg-config
# Trial 2: ignore pkg-config, leave OPENCV_CPPFLAGS and OPENCV_LDFLAGS empty,
# and try our own OPENCV_LIBS.
#
# If a prefix for OpenCV (arg 1) is given, trial 1 is skipped, and the given
# prefix is used in trial 2.
#
# OpenCV does not provide .la files (it is a CMake project), so the link tests
# below should be relatively reliable, at least when using OpenCV shared
# libraries on Linux or OS X. With a static-only OpenCV, it may be necessary to
# set the flags manually (including the transitive dependencies), at least on
# OS X.
AC_DEFUN([MM_LIB_OPENCV], [
   mm_lib_opencv_have_opencv=
   MM_LIB_CHECK_ARG_VARS([OPENCV], [OpenCV], [mm_lib_opencv_have_opencv=yes], [
      # fake loop (to exit with break)
      while true
      do
         AS_IF([test -n "$1"], [],
         [
            MM_LIB_SET_FLAGS_PKGCONFIG([OPENCV], [opencv], [],
            [
               # Trial 1
               _MM_LIB_OPENCV_IFELSE([with flags from pkg-config],
                                     [mm_lib_opencv_have_opencv=yes])
            ])
         ])
         test "x$mm_lib_opencv_have_opencv" = xyes && break

         # Trial 2
         MM_LIB_CLEAR_FLAGS([OPENCV])
         m4_ifval([$1], AS_IF([test -n "$1"],
         [
            OPENCV_CPPFLAGS="-I$1/include"
            OPENCV_LDFLAGS="-L$1/lib"
         ]))
         OPENCV_LIBS="-lopencv_highgui -lopencv_imgproc -lopencv_core"
         _MM_LIB_OPENCV_IFELSE([with hard-coded flags],
                               [mm_lib_opencv_have_opencv=yes])
         test "x$mm_lib_opencv_have_opencv" = xyes && break

         mm_lib_opencv_have_opencv=no
         # Do not leak failed configuration
         MM_LIB_CLEAR_FLAGS([OPENCV])
         break
      done
   ])
   AS_IF([test "x$mm_lib_opencv_have_opencv" = xyes], [$2], [$3])
])


#
# Sub-macros
#

AC_DEFUN([_MM_LIB_OPENCV_IFELSE], [
   MM_LIB_IFELSE([OPENCV], [OpenCV], [$1],
      [opencv/highgui.h], [cvGetCaptureProperty],
      [$2], [$3])
])
