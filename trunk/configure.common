# Check for programs.
AC_PROG_CXX([clang++ llvm-g++ g++ c++])
AC_PROG_CC([clang llvm-gcc gcc cc])
AC_PROG_LIBTOOL

case $host in
   *-*-linux*)
   LPTHREAD="-lpthread"
   DEVICEADAPTERPATH="\${libdir}/micro-manager"
   ;;
esac
AC_SUBST(LPTHREAD)

build_platform="$build_cpu"
case "$build_cpu" in
   i*86)
   build_platform="i386"
   ;;
esac

# Specify architecture when building Universal Binaries on Mac
AC_ARG_ENABLE([arch], [AS_HELP_STRING([--enable-arch=arch],[Mac OS X only: ppc, i386, or x86_64])], [ARCH=$enableval], [ARCH=])

#----------------------------------------------------------------
# ImageJ
#----------------------------------------------------------------

# Determine if we need to look for ImageJ
AC_MSG_CHECKING(whether to build for ImageJ)
AC_ARG_WITH([imagej], [AS_HELP_STRING([--with-imagej=path],[path to ImageJ directory])], [IMAGEJDIR="$withval"], [IMAGEJDIR="yes"])
AC_MSG_RESULT($IMAGEJDIR)

# Debian IJ settings
DEBIANIJ="/usr/share/java/ij.jar";
DEBIANIJPLUGIN="/usr/share/imagej";

AM_CONDITIONAL(BUILD_WITH_JAVA, test "$IMAGEJDIR" != "no")
if test "$IMAGEJDIR" != "no" ; then
   AC_MSG_CHECKING(for ImageJ directory)

   if test "$IMAGEJDIR" = "yes"; then
      IMAGEJDIR="/Applications/ImageJ /usr/local/ImageJ /usr/local/imagej "
   fi
   IJPATH=""
   for d in $IMAGEJDIR ; do
     if test -d "$d" ; then
       AC_MSG_RESULT($d)
       IJPATH=$d
       break
     fi
   done

   if test -e "$DEBIANIJ"; then
     IJPATH="$DEBIANIJPLUGIN";
   fi

   if test "$IJPATH" = "" ; then
     AC_MSG_ERROR([not found, use the --without-imagej option to build without ImageJ or install ImageJ (http://rsb.info.nih.gov/ij/ and rerun configure, possibly using the --with-imagej=path option])
   fi

   if test -z "$DEVICEADAPTERPATH"; then
      DEVICEADAPTERPATH="$IJPATH"
   fi

   if test -z "$JARPATH"; then
      JARPATH="$IJPATH/plugins/Micro-Manager"
   fi
else
   DEVICEADAPTERPATH="\${libdir}/micro-manager"
fi


AC_SUBST(IJPATH)
AC_SUBST(DEVICEADAPTERPATH)
AC_SUBST(JARPATH)

AC_MSG_CHECKING(device adapter path)
AC_MSG_RESULT($DEVICEADAPTERPATH)

AC_MSG_CHECKING(jar path)
AC_MSG_RESULT($JARPATH)
