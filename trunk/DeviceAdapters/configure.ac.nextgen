# This file is part of a progressive rewrite of the GNU (autotools) build
# scripts. Please do not modify this file. Instead, please modify the legacy
# build files (without the .nextgen suffix). Changes to legacy files will be
# automatically detected and manually applied to the next-gen files.
# - Mark Tsuchida
#
# %nextgen_build_filename = configure.ac
# %nextgen_build_replaces = configure.in e19f50963c84e60161e98fe080b3e6f1

AC_PREREQ([2.64])
AC_INIT([Micro-Manager], [1.4], [info@micro-manager.org])
AC_CONFIG_MACRO_DIR([../m4])
AC_CONFIG_SRCDIR([DemoCamera/DemoCamera.cpp])
AC_CANONICAL_HOST
AM_INIT_AUTOMAKE
AC_DISABLE_STATIC
AC_PROG_LIBTOOL

m4_include([../configure.common])

# TODO: Following should decide whether adapters depending on BOOST get build
AX_BOOST_BASE([1.40.0])
AX_BOOST_THREAD
AX_BOOST_ASIO
AX_BOOST_SYSTEM
AX_BOOST_CHRONO
AX_BOOST_DATE_TIME

# Find Micro-Manager headers
micromanager_path=${ac_pwd}/..
MMDEVAPI_CXXFLAGS="-I${micromanager_path}/MMDevice ${BOOST_CPPFLAGS}"
AC_SUBST(MMDEVAPI_CXXFLAGS)

# Find Micro-Manager static device library
## Note: adding the libtool library gives problems on OS X 10.4, adding the static lib works
MMDEVAPI_LIBADD="${micromanager_path}/MMDevice/.libs/libMMDevice.a"
AC_SUBST(MMDEVAPI_LIBADD)

# Apply appropriate libtool options for the Micro-Manager device API
MMDEVAPI_LDFLAGS="-module -shared -shrext \"\$(MMSUFFIX)\" -avoid-version"
AC_SUBST(MMDEVAPI_LDFLAGS)

# Location of third party public files
thirdpartypublic="${micromanager_path}/../3rdpartypublic"


# libusb 0.1 or libusb-compat
MM_ARG_WITH_OPTIONAL_LIB([libusb 0.1 or libusb-compat], [libusb-0-1], [LIBUSB_0_1])
AS_IF([test "x$want_libusb_0_1" != xno],
[
   MM_LIB_OPENCV([$LIBUSB_0_1_PREFIX],
   [
      use_libusb_0_1=yes
   ],
   [
      use_libusb_0_1=no
      AS_IF([test "x$want_libusb_0_1" = xyes],
            [MM_MSG_OPTIONAL_LIB_FAILURE([libusb 0.1 or libusb-compat], [libusb-0-1])])
   ])
],
[use_libusb_0_1=no])


AM_CONDITIONAL([BUILD_USBMANAGER], [test "x$use_libusb_0_1" = xyes])


# HIDAPI
MM_ARG_WITH_OPTIONAL_LIB([HIDAPI], [hidapi], [HIDAPI])
AS_IF([test "x$want_hidapi" != xno],
[
	MM_LIB_HIDAPI([$HIDAPI_PREFIX],
   [
      use_hidapi=yes
   ],
   [
      use_hidapi=no
      AS_IF([test "x$want_hidapi" = xyes],
            [MM_MSG_OPTIONAL_LIB_FAILURE([HIDAPI], [hidapi])])
   ])
],
[use_hidapi=no])


AM_CONDITIONAL([BUILD_HID], [test "x$use_hidapi" = xyes])


# Only build PVCAM if header files present.
AC_MSG_CHECKING(for Pvcam)
AC_FIND_FILE([pvcam.h], [/usr/include/pvcam /Library/Frameworks/PVCAM.framework/Headers], PVCAMINCDIR)
if test x$PVCAMINCDIR = xNO; then
   AC_MSG_RESULT([not found])
fi
AM_CONDITIONAL([BUILD_PVCAM], [test -f "$PVCAMINCDIR/pvcam.h"])
if test -f "$PVCAMINCDIR/pvcam.h" ; then
   AC_MSG_RESULT([found])
else
   AC_MSG_RESULT([not found])
fi

# Same for QCam
AC_MSG_CHECKING(for QCam)
AM_CONDITIONAL([BUILD_QCAM],[test -f "/Library/Frameworks/QCam.framework/Headers/QCamApi.h"])
if test -f "/Library/Frameworks/QCam.framework/Headers/QCamApi.h" ; then
   AC_MSG_RESULT([found])
else
   AC_MSG_RESULT([not found])
fi


# Same for Scion
AC_MSG_CHECKING(for Scion)
AM_CONDITIONAL([BUILD_SCION],[test -f "/Library/Frameworks/sfwlib.framework/Headers/sfwlib.h"])
if test -f "/Library/Frameworks/sfwlib.framework/Headers/sfwlib.h"; then
   AC_MSG_RESULT([found])
else
   AC_MSG_RESULT([not found])
fi

# Same for Diagnostic Spot
AC_MSG_CHECKING(for Diagnostic Spot)
AM_CONDITIONAL([BUILD_SPOT],[test -f "/Library/Frameworks/SpotCam.framework/Headers/SpotCam.h"])
if test -f "/Library/Frameworks/SpotCam.framework/Headers/SpotCam.h"; then
   AC_MSG_RESULT([found])
else
   AC_MSG_RESULT([not found])
fi


# libdc1394 2.x
MM_ARG_WITH_OPTIONAL_LIB([libdc1394], [libdc1394], [LIBDC1394])
AS_IF([test "x$want_libdc1394" != xno],
[
   MM_LIB_DC1394([$LIBDC1394_PREFIX],
   [
      use_libdc1394=yes
   ],
   [
      use_libdc1394=no
      AS_IF([test "x$want_libdc1394" = xyes],
            [MM_MSG_OPTIONAL_LIB_FAILURE([libdc1394], [libdc1394])])
   ])
],
[use_libdc1394=no])


# TODO These BUILD_x conditionals should not be associated with particular
# libraries; they might depend on multiple libraries. Should collect them at
# the end of this file
AM_CONDITIONAL([BUILD_DC1394], [test "x$use_libdc1394" = xyes])
# TODO BUILD_IIDC should also check for Boost libraries
AM_CONDITIONAL([BUILD_IIDC], [test "x$use_libdc1394" = xyes])


# Andor cameras on linux 
AC_MSG_CHECKING(for Andor cameras)
AM_CONDITIONAL([BUILD_ANDOR],[test -f "/usr/local/include/atmcdLXd.h"])
if test -f "/usr/local/include/atmcdLXd.h" ; then
   AC_MSG_RESULT([found])
else
   AC_MSG_RESULT([not found])
fi

# Andor laser combiner on linux 
AC_MSG_CHECKING(for Andor laser combiner)
AM_CONDITIONAL([BUILD_ANDORLASERCOMBINER],[test -f "/usr/local/include/ALC_REV.h"])
if test -f "/usr/local/include/ALC_REV.h" ; then
   AC_MSG_RESULT([found])
else
   AC_MSG_RESULT([not found])
fi

# Sensicam on linux 
AC_MSG_CHECKING(for Sensicam)
AM_CONDITIONAL([BUILD_SENSICAM],[test -f "/usr/local/include/sensicam.h"])
if test -f "/usr/local/include/sensicam.h"; then
   AC_MSG_RESULT([found])
else
   AC_MSG_RESULT([not found])
fi



# check for IDS_uEye header file and library
AC_MSG_CHECKING(for IDS_uEye header file and library)
AC_CHECK_HEADERS(ueye.h,
	[AC_CHECK_LIB(ueye_api,is_GetDLLVersion,IDSUEYE_PRESENT=true,IDSUEYE_PRESENT=false)],
	IDSUEYE_PRESENT=false)
AM_CONDITIONAL(BUILD_IDSUEYE, $IDSUEYE_PRESENT)

if [$IDSUEYE_PRESENT]; then
   AC_MSG_NOTICE([IDS_uEye present])
else
   AC_MSG_NOTICE([IDS_uEye not present])
fi



PKG_PROG_PKG_CONFIG
PKG_CHECK_MODULES([GPHOTO2], [libgphoto2 >= 2.5.1], [have_libgphoto2=yes], [have_libgphoto2=no])

# Libtool dynamic loading library (used by libgphoto2)
AC_MSG_CHECKING(for libltdl)
AC_ARG_WITH([ltdl], [AS_HELP_STRING([--with-ltdl=path], [use libltdl at path])],
   [LIBLTDLPATH="$withval"], [LIBLTDLPATH="yes"])
case $LIBLTDLPATH in
   yes)
      ltdl_libdirs="/usr/local/$ARCH/lib /usr/lib /usr/lib64"
      ;;
   no)
       AC_MSG_RESULT([libltdl required for GPhoto2 (SimpleCam) but disabled on command line])
      ltdl_libdirs=""
      ;;
   *)
      ltdl_libdirs="$LIBLTDLPATH"
      ;;
esac
# XXX This is broken on non-OS X! We shouldn't be using AC_FIND_FILE for a
# library anyway.
AC_FIND_FILE([libltdl.dylib], [$ltdl_libdirs], LIBLTDLPATH)
if test x$LIBLTDLPATH = xNO; then
   AC_MSG_RESULT(["libltdl not found, cannot build GPhoto2 (SimpleCam) adapter"])
else
   AC_MSG_RESULT(["libltdl" found])
   LIBLTDL="$LIBLTDLPATH/libltdl.dylib"

   # SimpleCam - GPhoto
   if test x$have_libgphoto2 = xno; then
      buildsc=false
      AC_MSG_RESULT([not found])
   else
      AC_MSG_RESULT([found])
      GPHOTO2_VERSION=`$PKG_CONFIG --modversion libgphoto2`
      GPHOTO2_PORT_VERSION=`$PKG_CONFIG --modversion libgphoto2_port`
      GPHOTO2_LIBDIR=`$PKG_CONFIG --variable=libdir libgphoto2`
      LIBGPHOTO2LIBDIR=`$PKG_CONFIG --variable=libdir libgphoto2`
      buildsc=false
      if test -f "$LIBGPHOTO2LIBDIR/libgphoto2.la"; then
         LIBGPHOTO2="$LIBGPHOTO2LIBDIR/libgphoto2.la $LIBLTDL"
         buildsc=true
      else
         if test -f "$LIBGPHOTO2LIBDIR/libgphoto2.so"; then
            LIBGPHOTO2="-lgphoto2"
            buildsc=true
         fi
      fi
   fi
fi


# On mac, install dynamic gphoto2 libraries 
case $host in
   *apple-darwin*)
      AC_SUBST([INSTALL_LIBGPHOTO], [install_macos])
      ;;
   *)
      AC_SUBST([INSTALL_LIBGPHOTO], [install_linux])
      ;;
esac
AC_SUBST(GPHOTO2_CFLAGS)
AC_SUBST(GPHOTO2_LIBS)
AC_SUBST(GPHOTO2_LIBDIR)
AC_SUBST(GPHOTO2_VERSION)
AC_SUBST(GPHOTO2_PORT_VERSION)
AC_SUBST(LIBGPHOTO2)
AC_SUBST(LIBLTDL)

# LibFreeImage
AC_MSG_CHECKING(for libFreeImage headers)
AC_FIND_FILE([FreeImage.h], [/usr/local/include /usr/include ${thirdpartypublic}/FreeImage/Dist], FREEIMAGEINCDIR)
AM_CONDITIONAL([LIBFREEIMAGEFOUND], [test -f "$FREEIMAGEINCDIR/FreeImage.h"])
if test -f "$FREEIMAGEINCDIR/FreeImage.h"; then
   AC_MSG_RESULT([found])
   
   AC_FIND_FILE([libfreeimage.a], [/usr/lib /usr/lib64 /usr/local/$ARCH/lib], FREEIMAGELIBDIR)
   if test x$FREEIMAGELIBDIR = xNO; then
      buildsc=false
      AC_MSG_RESULT(["libfreeimage.a" not found...])
   else
      AC_MSG_RESULT(["libfreeimage.a" found])
      AC_SUBST(LIBFREEIMAGE, "$FREEIMAGELIBDIR/libfreeimage.a")
      AC_SUBST(LIBFREEIMAGE_CXXFLAGS, "-I$FREEIMAGEINCDIR")
   fi
   
   AC_MSG_CHECKING(for libFreeImagePlus headers)
   AC_FIND_FILE([FreeImagePlus.h], [/usr/local/include /usr/include ${thirdpartypublic}/FreeImage/Wrapper/FreeImagePlus], FREEIMAGEPLUSINCDIR)
   if test -f "$FREEIMAGEPLUSINCDIR/FreeImagePlus.h"; then
      AC_MSG_RESULT([found])
      AC_SUBST(LIBFREEIMAGEPLUS_CXXFLAGS, "-I$FREEIMAGEPLUSINCDIR")
      AC_FIND_FILE([libfreeimageplus.a], [/usr/lib /usr/lib64 /usr/local/$ARCH/lib], FREEIMAGEPLUSLIBDIR)
      if test x$FREEIMAGEPLUSLIBDIR = xNO; then
         AC_MSG_RESULT(["libfreeimageplus.a" not found (can be ok)])
      else
         AC_MSG_RESULT(["libfreeimageplus.a" found])
         AC_SUBST(LIBFREEIMAGEPLUS, "$FREEIMAGEPLUSLIBDIR/libfreeimageplus.a")
      fi
   else
      AC_MSG_RESULT([not found])
   fi
fi
if test x$FREEIMAGELIBDIR = xNO; then
   buildsc=false
   AC_MSG_RESULT([not found: To build the GPhoto device adapter download and install FreeImage in ../../3rdpartypublic/FreeImage])
fi
AM_CONDITIONAL([BUILD_SIMPLECAM], [test x$buildsc = xtrue])

# Video4linux
AC_MSG_CHECKING(for Video4linux2)
AM_CONDITIONAL([BUILD_V4L2],[test -f "/usr/include/libv4l2.h"])
if test -f "/usr/include/libv4l2.h"; then
   AC_MSG_RESULT([found])
else
   AC_MSG_RESULT([not found])
fi

# ITC on linux 
AC_MSG_CHECKING(for ITC)
AM_CONDITIONAL([BUILD_ITC],[test -f "/usr/local/include/ITC18.h"])
if test -f "/usr/local/include/ITC18.h" ; then
   AC_MSG_RESULT([found])
else
   AC_MSG_RESULT([not found])
fi


# OpenCV
MM_ARG_WITH_OPTIONAL_LIB([OpenCV], [opencv], [OPENCV])
AS_IF([test "x$want_opencv" != xno],
[
   MM_LIB_OPENCV([$OPENCV_PREFIX],
   [
      use_opencv=yes
   ],
   [
      use_opencv=no
      AS_IF([test "x$want_opencv" = xyes],
            [MM_MSG_OPTIONAL_LIB_FAILURE([OpenCV], [opencv])])
   ])
],
[use_opencv=no])


AM_CONDITIONAL([BUILD_OPENCV], [test "x$use_opencv" = xyes])


# Only build ... when the code is there
AM_CONDITIONAL([BUILD_TEST],[test -f "Test/Test.h"])
if test -f "Test/Test.h"; then
   TEST=Test
   TESTMAKEFILE="Test/Makefile"
else
   TEST=
   TESTMAKEFILE=
fi
AC_SUBST(TEST)

# Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE


# On the Mac we need some frameworks that can be in the way on Linux
case $host in 
   *apple-darwin*)
      AC_MSG_CHECKING(pvcam linker flags)
      PVCAMFRAMEWORKS="-framework PVCAM"
      AC_MSG_RESULT($PVCAMFRAMEWORKS)
      AC_MSG_CHECKING(diagnostic spot linker flags)
      SPOTFRAMEWORKS="-framework SpotCam"
      AC_MSG_RESULT($SPOTFRAMEWORKS)
      AC_MSG_CHECKING(Serial adapter linker flags)
      SERIALFRAMEWORKS="-framework CoreFoundation -framework IOKit"
      AC_MSG_RESULT($SERIALFRAMEWORKS)
      AC_MSG_CHECKING(QuickTime linker flags)
      QUICKTIMEFRAMEWORKS="-framework QuickTime -framework QTKit"
      AC_MSG_RESULT($QUICKTIMEFRAMEWORKS)
      ;;
   *linux*)
      AC_MSG_CHECKING(pvcam linker flags)
      PVCAMFRAMEWORKS="-lpvcam"
      ;;
esac
AC_SUBST(PVCAMFRAMEWORKS)
AC_SUBST(SPOTFRAMEWORKS)
AC_SUBST(SERIALFRAMEWORKS)
AC_SUBST(QUICKTIMEFRAMEWORKS)

# Micro-Manager libraries have a prefix & suffix to make them unique.  These are defined here:
case $host in
   *-*-linux*)
   MMSUFFIX=".so.0"
   MMPREFIX="libmmgr_dal_"
   ;;
esac
if test -z "$MMSUFFIX"; then
  MMSUFFIX=""
fi
if test -z "$MMPREFIX"; then
  MMPREFIX="mmgr_dal_"
fi

AC_MSG_CHECKING(library suffix)
AC_MSG_RESULT($MMSUFFIX)
AC_SUBST(MMSUFFIX)
AC_MSG_CHECKING(library prefix)
AC_MSG_RESULT($MMPREFIX)
AC_SUBST(MMPREFIX)


# Checks for library functions.
AC_HEADER_STDC
AC_CHECK_FUNCS([memset])

AC_CONFIG_FILES(
	[Makefile]
	[AAAOTF/Makefile:AAAOTF/Makefile.in:MMDEVAPI.rules]
	[AOTF/Makefile:AOTF/Makefile.in:MMDEVAPI.rules]
	[Aladdin/Makefile:Aladdin/Makefile.in:MMDEVAPI.rules]
	[Andor/Makefile:Andor/Makefile.in:MMDEVAPI.rules]
	[AndorLaserCombiner/Makefile:AndorLaserCombiner/Makefile.in:MMDEVAPI.rules]
	[Aquinas/Makefile:Aquinas/Makefile.in:MMDEVAPI.rules]
	[Arduino/Makefile:Arduino/Makefile.in:MMDEVAPI.rules]
	[ASIFW1000/Makefile:ASIFW1000/Makefile.in:MMDEVAPI.rules]
	[ASIStage/Makefile:ASIStage/Makefile.in:MMDEVAPI.rules]
	[ASITiger/Makefile:ASITiger/Makefile.in:MMDEVAPI.rules]
	[ASIWPTR/Makefile:ASIWPTR/Makefile.in:MMDEVAPI.rules]
	[CARVII/Makefile:CARVII/Makefile.in:MMDEVAPI.rules]
	[Cobolt/Makefile:Cobolt/Makefile.in:MMDEVAPI.rules]
	[CoherentCube/Makefile:CoherentCube/Makefile.in:MMDEVAPI.rules]
	[Conix/Makefile:Conix/Makefile.in:MMDEVAPI.rules]
	[Corvus/Makefile:Corvus/Makefile.in:MMDEVAPI.rules]
	[CSUW1/Makefile:CSUW1/Makefile.in:MMDEVAPI.rules]
	[DTOpenLayer/Makefile:DTOpenLayer/Makefile.in:MMDEVAPI.rules]
	[DemoCamera/Makefile:DemoCamera/Makefile.in:MMDEVAPI.rules]
	[FreeSerialPort/Makefile:FreeSerialPort/Makefile.in:MMDEVAPI.rules]
	[FocalPoint/Makefile:FocalPoint/Makefile.in:MMDEVAPI.rules]
	[IDS_uEye/Makefile:IDS_uEye/Makefile.in:MMDEVAPI.rules]
	[IIDC/Makefile:IIDC/Makefile.in:MMDEVAPI.rules]
	[ITC18/Makefile:ITC18/Makefile.in:MMDEVAPI.rules]
	[ImageProcessorChain/Makefile:ImageProcessorChain/Makefile.in:MMDEVAPI.rules]
	[K8055/Makefile:K8055/Makefile.in:MMDEVAPI.rules]
	[K8061/Makefile:K8061/Makefile.in:MMDEVAPI.rules]
	[LeicaDMI/Makefile:LeicaDMI/Makefile.in:MMDEVAPI.rules]
	[LeicaDMR/Makefile:LeicaDMR/Makefile.in:MMDEVAPI.rules]
	[LeicaDMSTC/Makefile:LeicaDMSTC/Makefile.in:MMDEVAPI.rules]
	[Ludl/Makefile:Ludl/Makefile.in:MMDEVAPI.rules]
	[LudlLow/Makefile:LudlLow/Makefile.in:MMDEVAPI.rules]
	[LumencorSpectra/Makefile:LumencorSpectra/Makefile.in:MMDEVAPI.rules]
	[MaestroServo/Makefile:MaestroServo/Makefile.in:MMDEVAPI.rules]
	[Marzhauser/Makefile:Marzhauser/Makefile.in:MMDEVAPI.rules]
	[Marzhauser-LStep/Makefile:Marzhauser-LStep/Makefile.in:MMDEVAPI.rules]
   [MicroPoint/Makefile:MicroPoint/Makefile.in:MMDEVAPI.rules]
   [Motic_mac/Makefile:Motic_mac/Makefile.in:MMDEVAPI.rules]
	[MP285/Makefile:MP285/Makefile.in:MMDEVAPI.rules]
	[Neos/Makefile:Neos/Makefile.in:MMDEVAPI.rules]
	[Nikon/Makefile:Nikon/Makefile.in:MMDEVAPI.rules]
	[NikonTE2000/Makefile:NikonTE2000/Makefile.in:MMDEVAPI.rules]
	[nPoint/Makefile:nPoint/Makefile.in:MMDEVAPI.rules]
	[OpenCVgrabber/Makefile:OpenCVgrabber/Makefile.in:MMDEVAPI.rules]
	[Omicron/Makefile:Omicron/Makefile.in:MMDEVAPI.rules]
	[Oxxius/Makefile:Oxxius/Makefile.in:MMDEVAPI.rules]
   [ParallelPort/Makefile:ParallelPort/Makefile.in:MMDEVAPI.rules]
	[Pecon/Makefile:Pecon/Makefile.in:MMDEVAPI.rules]
	[pgFocus/Makefile:pgFocus/Makefile.in:MMDEVAPI.rules]
	[PI/Makefile:PI/Makefile.in:MMDEVAPI.rules]
	[PI_GCS/Makefile:PI_GCS/Makefile.in:MMDEVAPI.rules]
	[PI_GCS_2/Makefile:PI_GCS_2/Makefile.in:MMDEVAPI.rules]
	[Piezosystem_30DV50/Makefile:Piezosystem_30DV50/Makefile.in:MMDEVAPI.rules]
	[Piezosystem_dDrive/Makefile:Piezosystem_dDrive/Makefile.in:MMDEVAPI.rules]
	[Piezosystem_NV120_1/Makefile:Piezosystem_NV120_1/Makefile.in:MMDEVAPI.rules]
	[Piezosystem_NV40_1/Makefile:Piezosystem_NV40_1/Makefile.in:MMDEVAPI.rules]
	[Piezosystem_NV40_3/Makefile:Piezosystem_NV40_3/Makefile.in:MMDEVAPI.rules]
	[PVCAM/Makefile:PVCAM/Makefile.in:MMDEVAPI.rules]
	[PrecisExcite/Makefile:PrecisExcite/Makefile.in:MMDEVAPI.rules]
	[PrincetonInstruments/Makefile:PrincetonInstruments/Makefile.in:MMDEVAPI.rules]
	[Prior/Makefile:Prior/Makefile.in:MMDEVAPI.rules]
	[PriorLegacy/Makefile:PriorLegacy/Makefile.in:MMDEVAPI.rules]
	[QCam/Makefile:QCam/Makefile.in:MMDEVAPI.rules]
   [Scientifica/Makefile:Scientifica/Makefile.in:MMDEVAPI.rules]
	[Sapphire/Makefile:Sapphire/Makefile.in:MMDEVAPI.rules]
	[ScionCam/Makefile:ScionCam/Makefile.in:MMDEVAPI.rules]
	[Sensicam/Makefile:Sensicam/Makefile.in:MMDEVAPI.rules]
	[SimpleCam/Makefile:SimpleCam/Makefile.in:MMDEVAPI.rules]
	[SpectralLMM5/Makefile:SpectralLMM5/Makefile.in:MMDEVAPI.rules]
	[Spot/Makefile:Spot/Makefile.in:MMDEVAPI.rules]
	[SutterLambda/Makefile:SutterLambda/Makefile.in:MMDEVAPI.rules]
	[SutterStage/Makefile:SutterStage/Makefile.in:MMDEVAPI.rules]
	[SerialManager/Makefile:SerialManager/Makefile.in:MMDEVAPI.rules]
	[SimpleAutofocus/Makefile:SimpleAutofocus/Makefile.in:MMDEVAPI.rules]
	[Thorlabs/Makefile:Thorlabs/Makefile.in:MMDEVAPI.rules]
	[ThorlabsDCxxxx/Makefile:ThorlabsDCxxxx/Makefile.in:MMDEVAPI.rules]
	[ThorlabsFilterWheel/Makefile:ThorlabsFilterWheel/Makefile.in:MMDEVAPI.rules]
	[ThorlabsSC10/Makefile:ThorlabsSC10/Makefile.in:MMDEVAPI.rules]
	[Tofra/Makefile:Tofra/Makefile.in:MMDEVAPI.rules]
	[USBManager/Makefile:USBManager/Makefile.in:MMDEVAPI.rules]
	[HIDManager/Makefile:HIDManager/Makefile.in:MMDEVAPI.rules]
   [Utilities/Makefile:Utilities/Makefile.in:MMDEVAPI.rules]
   [VariLC/Makefile:VariLC/Makefile.in:MMDEVAPI.rules]
	[Video4Linux/Makefile:Video4Linux/Makefile.in:MMDEVAPI.rules]
	[Vincent/Makefile:Vincent/Makefile.in:MMDEVAPI.rules]
   [Vortran/Makefile:Vortran/Makefile.in:MMDEVAPI.rules]
	[XCite120PC_Exacte/Makefile:XCite120PC_Exacte/Makefile.in:MMDEVAPI.rules]
	[XCiteLed/Makefile:XCiteLed/Makefile.in:MMDEVAPI.rules]
	[Xcite/Makefile:Xcite/Makefile.in:MMDEVAPI.rules]
	[XLight/Makefile:XLight/Makefile.in:MMDEVAPI.rules]
	[Yokogawa/Makefile:Yokogawa/Makefile.in:MMDEVAPI.rules]
	[Zaber/Makefile:Zaber/Makefile.in:MMDEVAPI.rules]
	[ZeissCAN/Makefile:ZeissCAN/Makefile.in:MMDEVAPI.rules]
	[ZeissCAN29/Makefile:ZeissCAN29/Makefile.in:MMDEVAPI.rules]
	[dc1394/Makefile:dc1394/Makefile.in:MMDEVAPI.rules]
	[kdv/Makefile:kdv/Makefile.in:MMDEVAPI.rules]
)

AC_OUTPUT
